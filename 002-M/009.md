Savory Navy Cheetah

medium

# Vault deposit DoS through totalDeposit inflation

## Summary

The accounting of the `withdrawDepositEquivalent` amount in the Vault's `_initiateWithdraw` function does not cover the case of withdrawing tokens transferred by another liquidity provider.

## Vulnerability Detail

The Vault keeps track of tokens that were withdrawn, so they aren't counted anymore in the the `maxDeposit` check:

```Solidity
File: Vault.sol
487:         // -----------------------------
488:         // A withdrawal pre-order free space for further deposits, hence we must
489:         // update the vault capacity.
490:         //
491:         // The deposit receipt must also be updated in order to correctly update
492:         // the vault total deposits, shall the user initiate other withdrawal
493:         // pre-orders in the same epoch (as it is used for such computation).
494:         //
495:         // Steps:
496:         //   - estimate the increased capacity by computing the following proportion:
497:         //       withdrawed_shares : user_shares = x : user_deposits
498:         //   - use the found number for decreasing the current number of deposits.
499:         //   - update the user's deposit receipt [cumulativeAmount] value.
500:         VaultLib.DepositReceipt storage depositReceipt = depositReceipts[msg.sender];
501:         // NOTE: the user deposits to consider are only the ones for which a share has been minted.
502:         uint256 userDeposits = depositReceipt.cumulativeAmount;
503:         if (depositReceipt.epoch == epoch.current) {
504:             userDeposits -= depositReceipt.amount;
505:         }
506: 
507:         uint256 withdrawDepositEquivalent = (userDeposits * shares) / userShares;
508: 
509:         _state.liquidity.totalDeposit -= withdrawDepositEquivalent;
510:         depositReceipt.cumulativeAmount -= withdrawDepositEquivalent;
```

If we consider the case of a user withdrawing shares that were transferred to them, it may very well be that their `depositReceipt.cumulativeAmount` is zero, so the withdrawal of any amount of tokens will have no impact on the vault's `_state.liquidity.totalDeposit`.

The following Foundry test can be added to the `VaultShares.t.sol` to show the problem:

```Solidity
    function testWithdrawTransferredTokens() public {
        // Alice deposits 100
        VaultUtils.addVaultDeposit(alice, 100, tokenAdmin, address(vault), vm);

        Utils.skipDay(false, vm);
        vm.prank(tokenAdmin);
        vault.rollEpoch();

        // Alice redeems all tokens, but lets Bob withdraw them
        vm.startPrank(alice);
        vault.redeem(100);
        vault.transfer(bob, 100);
        vm.stopPrank();
        
        vm.prank(bob);
        vault.initiateWithdraw(100);

        Utils.skipDay(false, vm);
        vm.prank(tokenAdmin);
        vault.rollEpoch();

        vm.prank(bob);
        vault.completeWithdraw();

        // At this point, all tokens are redeemed, but totalDeposit is still 100
        // as if no tokens were withdrawn
        assertEq(100, baseToken.balanceOf(address(bob)));
        assertEq(100, VaultUtils.vaultState(vault).liquidity.totalDeposit);
    }
```

This incorrect accounting can build up over time, inflating (or, rather, preventing the correct deflation of) the vault's `totalDeposit`, eventually reaching `maxDeposit`, that is when the vault will stop accepting deposits.

## Impact

The vault's `totalDeposit` can be ever-inflated to keep tracking withdrawn tokens. When the inflation is large enough to reach the Vault's `maxDeposit`, the Vault will refuse deposits that would otherwise be accepted if accounting was correct.

## Code Snippet

https://github.com/sherlock-audit/2024-02-smilee-finance/blob/3241f1bf0c8e951a41dd2e51997f64ef3ec017bd/smilee-v2-contracts/src/Vault.sol#L500-L510

## Tool used

Manual Review

## Recommendation

This accounting vulnerability would best be fixed with an intervention on Vault share transfers, by either:
- preventing them altogether, or
- adjusting the sender and receiver's `depositReceipts` at transfer time