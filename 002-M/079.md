Bent Crimson Goat

medium

# The invariant `maxDeposit` for a `Vault` can be exceeded.

## Summary

The aggregate [`_state.liquidity.totalDeposit`](https://github.com/sherlock-audit/2024-02-smilee-finance/blob/3241f1bf0c8e951a41dd2e51997f64ef3ec017bd/smilee-v2-contracts/src/lib/VaultLib.sol#L29) of a [`Vault`](https://github.com/sherlock-audit/2024-02-smilee-finance/blob/main/smilee-v2-contracts/src/Vault.sol) can be exceeded due to dependence on a caller's balance, which can be manipulated.

## Vulnerability Detail

Shares in a [`Vault`](https://github.com/sherlock-audit/2024-02-smilee-finance/blob/main/smilee-v2-contracts/src/Vault.sol) represent a claim to a specific user's [`cumulativeAmount`](https://github.com/sherlock-audit/2024-02-smilee-finance/blob/3241f1bf0c8e951a41dd2e51997f64ef3ec017bd/smilee-v2-contracts/src/lib/VaultLib.sol#L43) of a [`DepositReceipt`](https://github.com/sherlock-audit/2024-02-smilee-finance/blob/3241f1bf0c8e951a41dd2e51997f64ef3ec017bd/smilee-v2-contracts/src/lib/VaultLib.sol#L39).

The mutable `balanceOf` that user directly influences the proportion of how much both their [`cumulativeAmount`](https://github.com/sherlock-audit/2024-02-smilee-finance/blob/3241f1bf0c8e951a41dd2e51997f64ef3ec017bd/smilee-v2-contracts/src/lib/VaultLib.sol#L43) and global [`_state.liquidity.totalDeposit`](https://github.com/sherlock-audit/2024-02-smilee-finance/blob/3241f1bf0c8e951a41dd2e51997f64ef3ec017bd/smilee-v2-contracts/src/lib/VaultLib.sol#L29) is decreased.

Let's start by looking through the associated [`initiateWithdraw(uint256)`](https://github.com/sherlock-audit/2024-02-smilee-finance/blob/3241f1bf0c8e951a41dd2e51997f64ef3ec017bd/smilee-v2-contracts/src/Vault.sol#L449) flow:

```solidity
// We take advantage of this flow in order to also transfer any unredeemed share to the user.
_redeem(0, true);
// NOTE: since we made a 'redeem all', from now on all the user's shares are owned by him.
@> uint256 userShares = balanceOf(msg.sender);

// ...

VaultLib.DepositReceipt storage depositReceipt = depositReceipts[msg.sender];
// NOTE: the user deposits to consider are only the ones for which a share has been minted.
uint256 userDeposits = depositReceipt.cumulativeAmount;
if (depositReceipt.epoch == epoch.current) {
    userDeposits -= depositReceipt.amount;
}

@> uint256 withdrawDepositEquivalent = (userDeposits * shares) / userShares;

_state.liquidity.totalDeposit -= withdrawDepositEquivalent;
depositReceipt.cumulativeAmount -= withdrawDepositEquivalent;
// -----------------------------

_state.withdrawals.newHeldShares += shares;

_transfer(msg.sender, address(this), shares);

// ...
```

We must draw attention to the fact that here, vault share `balanceOf(msg.sender)` directly impacts how much of their `withdrawDepositEquivalent` is removed. This value is under complete control of the `msg.sender`.

By way of example, consider when a user's share balance consists only of a single share (having transferred the rest of their shares to cold storage, for example); the entirety of their `withdrawDepositEquivalent` would be erased:

```solidity
uint256 withdrawDepositEquivalent = (userDeposits * shares) / userShares; // @audit == (1 * shares / 1) == shares
```

Only a single share is burned, but the totality of their `withdrawDepositEquivalent` is reduced. This tricks the vault into believing that the user has burned their entire balance.

These shares can subsequently be brought back into the user's account.

> [!NOTE]
> There is no loss for the `msg.sender` in doing this.
>
> Their `depositReceipt.cumulativeAmount` is only a passive variable which reports the current interpreted state. They can continue to execute subsequent withdraws which continue to [properly convert into the redemption of real underlying value](https://github.com/sherlock-audit/2024-02-smilee-finance/blob/3241f1bf0c8e951a41dd2e51997f64ef3ec017bd/smilee-v2-contracts/src/Vault.sol#L543).

Therefore, the [`Vault`](https://github.com/sherlock-audit/2024-02-smilee-finance/blob/main/smilee-v2-contracts/src/Vault.sol) operates under the assumption that it has less capital deposited than in actuality, and will continue to accept more deposits over and above the intended maximum.

Likewise, for excessively large values of `userShares` compared to `userDeposits * shares` (for example those acquired via a flash loan), `withdrawDepositEquivalent` can truncate to zero, meaning it is possible to DoS the pool, since `_state.liquidity.totalDeposit` can remain artificially high whilst a malicious user can exit their position.

However, this is lesser severity as an admin can merely increase the globally-cumulative [`maxDeposit`](https://github.com/sherlock-audit/2024-02-smilee-finance/blob/3241f1bf0c8e951a41dd2e51997f64ef3ec017bd/smilee-v2-contracts/src/Vault.sol#L36) via a call to [`setMaxDeposit(uint256)`](https://github.com/sherlock-audit/2024-02-smilee-finance/blob/3241f1bf0c8e951a41dd2e51997f64ef3ec017bd/smilee-v2-contracts/src/Vault.sol#L184).

## Impact

Broken protocol invariant due to overt dependence of a user-controlled value (medium).

## Code Snippet

```solidity
function _initiateWithdraw(uint256 shares, bool isMax) internal {
    // We take advantage of this flow in order to also transfer any unredeemed share to the user.
    _redeem(0, true);
    // NOTE: since we made a 'redeem all', from now on all the user's shares are owned by him.
    uint256 userShares = balanceOf(msg.sender);

    if (isMax) {
        shares = userShares;
    }

    if (shares == 0) {
        revert AmountZero();
    }

    if (shares > userShares) {
        revert ExceedsAvailable();
    }

    Epoch memory epoch = getEpoch();
    VaultLib.Withdrawal storage withdrawal = withdrawals[msg.sender];

    // If there is a pre-ordered withdrawal in the past, the user must first complete it.
    if (withdrawal.epoch < epoch.current && withdrawal.shares > 0) {
        revert ExistingIncompleteWithdraw();
    }

    // Update user withdrawal receipt:
    // NOTE: the withdrawal.shares value is zeroed when the user complete a withdraw.
    // NOTE: if there is a pre-ordered withdrawal in the current epoch, it is increased; otherwise it starts from zero.
    withdrawal.shares = withdrawal.shares + shares;
    withdrawal.epoch = epoch.current;

    // -----------------------------
    // A withdrawal pre-order free space for further deposits, hence we must
    // update the vault capacity.
    //
    // The deposit receipt must also be updated in order to correctly update
    // the vault total deposits, shall the user initiate other withdrawal
    // pre-orders in the same epoch (as it is used for such computation).
    //
    // Steps:
    //   - estimate the increased capacity by computing the following proportion:
    //       withdrawed_shares : user_shares = x : user_deposits
    //   - use the found number for decreasing the current number of deposits.
    //   - update the user's deposit receipt [cumulativeAmount] value.
    VaultLib.DepositReceipt storage depositReceipt = depositReceipts[msg.sender];
    // NOTE: the user deposits to consider are only the ones for which a share has been minted.
    uint256 userDeposits = depositReceipt.cumulativeAmount;
    if (depositReceipt.epoch == epoch.current) {
        userDeposits -= depositReceipt.amount;
    }

    uint256 withdrawDepositEquivalent = (userDeposits * shares) / userShares;

    _state.liquidity.totalDeposit -= withdrawDepositEquivalent;
    depositReceipt.cumulativeAmount -= withdrawDepositEquivalent;
    // -----------------------------

    _state.withdrawals.newHeldShares += shares;

    _transfer(msg.sender, address(this), shares);

    emit InitiateWithdraw(shares);
}
```

## Tool used

Foundry

## Recommendation

It is recommended that the `withdrawDepositEquivalent` computed is a function of the shares that will be burned, and not based on the current balance of the user, since this can be intentionally maliciously manipulated or even unintentionally when normalized across the aggregate behaviour of multiple users over time.

