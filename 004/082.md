Interesting Lace Cottonmouth

medium

# `Vault::_initiateWithdraw` can prevent users from withdrawing due to underflow

## Summary
When users deposit into the vault, `_state.liquidity.totalDeposit` is incremented with the amount of basetoken (USDC) the user deposits. A `depositReceipt` is then created for the user, which allows them to redeem its shares or withdraw liquidity. However, when the user withdraws, `_state.liquidity.totalDeposit` can underflow, causing the withdraw to revert.

## Vulnerability Detail
As stated in the summary, when a user deposits into the vault, `_state.liquidity.totalDeposit` is incremented by the amount they deposit. When a user withdraws by calling `initiateWithdraw`, the internal `_initiateWithdraw` function is called, which proceeds to execute the withdrawal. The following lines are executed:

```javascript
  uint256 userShares = balanceOf(msg.sender);
  VaultLib.DepositReceipt storage depositReceipt = depositReceipts[msg.sender];
  uint256 userDeposits = depositReceipt.cumulativeAmount;
  uint256 withdrawDepositEquivalent = (userDeposits * shares) / userShares;
  _state.liquidity.totalDeposit -= withdrawDepositEquivalent;
```
The amount that they withdraw may not be the same as the amount they deposited. The user deposits are cumulative, as we see in `userDeposits = depositReceipt.cumulativeAmount` and the formula `(userDeposits * shares) / userShares` can result in a larger value than what the `_state.liquidity.totalDeposit` holds, since users can withdraw more in return than what they deposited initially. This can cause underflow when the subtraction is executed, which will cause the entire transaction to revert. 

The `_initiateWithdraw` function is also called in `rescueShares`, where the same problem may occur.

## Impact
Users will not be able to withdraw liquidity.

## Code Snippet

Note: `_state.liquidity.totalDeposit` is of type `uint256`.

`Vault::deposit` [#L324-349](https://github.com/sherlock-audit/2024-02-smilee-finance/blob/main/smilee-v2-contracts/src/Vault.sol#L324-L349)
```javascript
    function deposit(uint256 amount, address receiver, uint256 accessTokenId) external isNotDead whenNotPaused {
        _checkEpochNotFinished();

        if (amount == 0) {
            revert AmountZero();
        }

        // Avoids underflows when the maxDeposit is setted below than the totalDeposit
        if (_state.liquidity.totalDeposit > maxDeposit) {
            revert ExceedsMaxDeposit();
        }

        if (amount > maxDeposit - _state.liquidity.totalDeposit) {
            revert ExceedsMaxDeposit();
        }

        _usePriorityAccess(amount, receiver, accessTokenId);

        _state.liquidity.pendingDeposits += amount;
@>      _state.liquidity.totalDeposit += amount;
        _emitUpdatedDepositReceipt(receiver, amount);

        IERC20(baseToken).safeTransferFrom(msg.sender, address(this), amount);

        emit Deposit(amount);
    }
```

`Vault::initiateWithdraw` [#L449-453](https://github.com/sherlock-audit/2024-02-smilee-finance/blob/main/smilee-v2-contracts/src/Vault.sol#L449-L453)
```javascript
    function initiateWithdraw(uint256 shares) external whenNotPaused isNotDead {
        _checkEpochNotFinished();

 @>    _initiateWithdraw(shares, false);
    }
 ```

`Vault::_initiateWithdraw` [#L455-518](https://github.com/sherlock-audit/2024-02-smilee-finance/blob/main/smilee-v2-contracts/src/Vault.sol#L455-L518)
```javascript
    function _initiateWithdraw(uint256 shares, bool isMax) internal {
        // We take advantage of this flow in order to also transfer any unredeemed share to the user.
        _redeem(0, true);
        // NOTE: since we made a 'redeem all', from now on all the user's shares are owned by him.
 @>    uint256 userShares = balanceOf(msg.sender);

        if (isMax) {
            shares = userShares;
        }

        if (shares == 0) {
            revert AmountZero();
        }

        if (shares > userShares) {
            revert ExceedsAvailable();
        }

        Epoch memory epoch = getEpoch();
        VaultLib.Withdrawal storage withdrawal = withdrawals[msg.sender];

        // If there is a pre-ordered withdrawal in the past, the user must first complete it.
        if (withdrawal.epoch < epoch.current && withdrawal.shares > 0) {
            revert ExistingIncompleteWithdraw();
        }

        // Update user withdrawal receipt:
        // NOTE: the withdrawal.shares value is zeroed when the user complete a withdraw.
        // NOTE: if there is a pre-ordered withdrawal in the current epoch, it is increased; otherwise it starts from zero.
        withdrawal.shares = withdrawal.shares + shares;
        withdrawal.epoch = epoch.current;

        // -----------------------------
        // A withdrawal pre-order free space for further deposits, hence we must
        // update the vault capacity.
        //
        // The deposit receipt must also be updated in order to correctly update
        // the vault total deposits, shall the user initiate other withdrawal
        // pre-orders in the same epoch (as it is used for such computation).
        //
        // Steps:
        //   - estimate the increased capacity by computing the following proportion:
        //       withdrawed_shares : user_shares = x : user_deposits
        //   - use the found number for decreasing the current number of deposits.
        //   - update the user's deposit receipt [cumulativeAmount] value.
        VaultLib.DepositReceipt storage depositReceipt = depositReceipts[msg.sender];
        // NOTE: the user deposits to consider are only the ones for which a share has been minted.
@>    uint256 userDeposits = depositReceipt.cumulativeAmount;
        if (depositReceipt.epoch == epoch.current) {
            userDeposits -= depositReceipt.amount;
        }

@>     uint256 withdrawDepositEquivalent = (userDeposits * shares) / userShares;

@>      _state.liquidity.totalDeposit -= withdrawDepositEquivalent;
         depositReceipt.cumulativeAmount -= withdrawDepositEquivalent;
        // -----------------------------

        _state.withdrawals.newHeldShares += shares;

        _transfer(msg.sender, address(this), shares);

        emit InitiateWithdraw(shares);
    }

```

## Tool used
Manual Review

## Recommendation
Since `_state.liquidity.totalDeposit` is used to check if it exceeds `maxDeposit` when a user deposits, my suggestion is to implement the following check when withdrawing:

```javascript
// Do not subtract if withdrawDepositEquivalent is greater than  _state.liquidity.totalDeposit, since it will underflow
if( _state.liquidity.totalDeposit  < withdrawDepositEquivalent){
      _state.liquidity.totalDeposit = 0;
 }
 else{
  _state.liquidity.totalDeposit -= withdrawDepositEquivalent;
  }
```

You can also consider using a separate variable for tracking totalWithdrawal.
 