Massive Alabaster Dalmatian

high

# Trader's position may be locked in DVP

## Summary
Trader's position may be locked in DVP because of out-of-date strike information

## Vulnerability Detail
When trader tries to mint in PositionManger, traders will provide strike as one parameter. And this parameter will be recorded as one part of **_positions** information. 
```solidity
    function mint(
        IPositionManager.MintParams calldata params
    ) external override returns (uint256 tokenId, uint256 premium) {
        ,,,
        // Premium already include fee
        baseToken.safeApprove(params.dvpAddr, obtainedPremium);
        premium = dvp.mint(
            address(this),
            params.strike,
            params.notionalUp,
            params.notionalDown,
            params.expectedPremium,
            params.maxSlippage,
            params.nftAccessTokenId
        );

        if (obtainedPremium > premium) {
            baseToken.safeTransferFrom(address(this), msg.sender, obtainedPremium - premium);
        }

        if (params.tokenId == 0) {
            // Mint token:
            tokenId = _nextId++;
            _mint(params.recipient, tokenId);

            Epoch memory epoch = dvp.getEpoch();

            // Save position:
            _positions[tokenId] = ManagedPosition({
                dvpAddr: params.dvpAddr,
 @==>strike in param is recorded
                strike: params.strike,
                expiry: epoch.current,
                premium: premium,
                leverage: (params.notionalUp + params.notionalDown) / premium,
                notionalUp: params.notionalUp,
                notionalDown: params.notionalDown,
                cumulatedPayoff: 0
            });
        } 
        ...
    }
```
When dvp.mint() is called in PositionManger::mint(), strike from parameter is not used, which could cause actual strike in DVP is different strike in PositionManger::_positions.

```solidity
    function mint(
        address recipient,
        uint256 strike,
        uint256 amountUp,
        uint256 amountDown,
        uint256 expectedPremium,
        uint256 maxSlippage,
        uint256 nftAccessTokenId
    ) external override returns (uint256 premium_) {
@ ==> ignore original strike.
        strike;
        _checkNFTAccess(nftAccessTokenId, recipient, amountUp + amountDown);
        Amount memory amount_ = Amount({up: amountUp, down: amountDown});

        premium_ = _mint(recipient, financeParameters.currentStrike, amount_, expectedPremium, maxSlippage);
    }
```
After that, when traders want to burn their position, strike from _positions will be passed into DVP::mint(), DVP might not find information about strike from _position.

```solidity
    function _sell(
        uint256 tokenId,
        uint256 notionalUp,
        uint256 notionalDown,
        uint256 expectedMarketValue,
        uint256 maxSlippage
    ) internal returns (uint256 payoff_) {
        ManagedPosition storage position = _positions[tokenId];
       ...
        // NOTE: the DVP already checks that the burned notional is lesser or equal to the position notional.
        // NOTE: the payoff is transferred directly from the DVP
        //@johnson, [H] if mint incorrect strike, funds will be strucked in vault forever. or other people 
        payoff_ = IDVP(position.dvpAddr).burn(
            position.expiry,
            msg.sender,
  @ ==> strike from mint's parameter  
            position.strike,
            notionalUp,
            notionalDown,
            expectedMarketValue,
            maxSlippage
        );
        ....
    }
```
In DVP, wrong strike information can cause non-exist position, and operation will be reverted.
```solidity
    function _burn(
        uint256 expiry,
        address recipient,
        uint256 strike,
        Amount memory amount,
        uint256 expectedMarketValue,
        uint256 maxSlippage
    ) internal returns (uint256 paidPayoff) {
        _requireNotPaused();
        Position.Info storage position = _getPosition(expiry, msg.sender, strike);
        if (!position.exists()) {
            revert PositionNotFound();
        }
}
```

Traders' strike parameter in PositionManager::mint is different actual strike information is possible in some special case.
eg.
- Epoch n is almost finished.
- Alice wants to mint one position through PositionManger, fetch current strike information.
- Alice mints position. At the almost same time, Epoch n is finished and admin triggers another epoch, and actual strike information is updated.
- Considering network delay, Alice's mint operation might be operated after new epoch.

## Impact
Traders' position will be locked in DVP and cannot be burned.

## Code Snippet
https://github.com/sherlock-audit/2024-02-smilee-finance/blob/main/smilee-v2-contracts/src/periphery/PositionManager.sol#L91-L137
https://github.com/sherlock-audit/2024-02-smilee-finance/blob/main/smilee-v2-contracts/src/IG.sol#L62-L76
https://github.com/sherlock-audit/2024-02-smilee-finance/blob/main/smilee-v2-contracts/src/periphery/PositionManager.sol#L215-L243
## Tool used

Manual Review

## Recommendation
After DVP's successful mint position, actual strike information should be returned to PositionManger.