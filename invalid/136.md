Flat Pineapple Pangolin

high

# Vault losses due to DEX slippage are not fairly shared between depositors

## Summary

Deposits and withdrawals trigger vault rebalancing, which is subject to DEX slippage. Each slipped DEX transaction loses some money to the vault. This is not taken into account in the design, and will create undesired consequences, including more active liquidity providers effectively offloading most of the losses to the less active LPs, and failure to withdraw the full shares for the last depositor.

## Vulnerability Detail

The number of tokens to rebalance is decided in `_adjustBalances` by calling either `exchange.getInputAmount` or `exchange.getOutputAmount`. Both of these functions compute the expected amount via Oracle price,  without taking into account the slippage. To be clear, "slippage" in the context of Smilee Finance is used as a catch-all term that includes both the swap fees and the price impact from the swap, and  as such is expected to be significant (on the order of 0.1 to 1.0%).

When an LP withdraws from the vault, they trigger a vault rebalance during the `rollEpoch` function, because the vault frees up a number of based tokens the LP can subsequently withdraw. The number of base tokens the LP gets is decided via fair price of their shares multiplied by the number of shares. This does not take into account slippage.  In contrast, the actual freeing of the tokens is affected by slippage.

For a simple example consider:
* LP A has 100 shares (100 base tokens at fair price)
* LP B has 100 shares (same)
* Vault has 100 base and 100 side tokens, the base/side price is 1.0

When the LP A initiates withdraws with their 100 shares, the vault rebalances so that it has 100 base tokens to release. To do that, it sells some side tokens. If there's non-zero slippage, it needs to sell 51 side tokens. After the LP A claims their assets, the status is:
* LP B has 100 shares (still 100 base tokens at fair price)
* Vault has 50 base tokens and 49 side tokens

When LP B initiates the withdraw, rollEpoch is going to fail because 100 base tokens cannot be withdrawn. If `emergencyRebalance` is called, then vault sells 49 side tokens to 48 base tokens, and ends up with 98 base tokens. The LP B has lost 2% of their capital, while the LP A has lost nothing. 

The slippage issue was raised in the Trust Security audit, in the context of delta hedging required by the IG product, and as such acknowledged by the team. In the reply to the Trust audit the Smilee Finance argues that:
> Given the edge nature of such scenarios, for this first version of the protocol we decided to implement reverts with a specific error so that the admin can assess the problem and fill the gap with some base tokens. Similarly users may overcome any malfunctioning by sending some base tokens to the vault.
> We are aware relying on manual process is not optimal, however fixing the issue by updating vault liabilities would have caused code complexity to grow extensively, introducing high failing risks and decreasing code readability.

There are indeed mitigations present in the code, for example:
* Swaps revert if they slip more that a baseline (2% by default). However, the PoC below demonstrates that the issue will cause some LPs to lose almost all of their capital even when the DEX slippage is smaller than that.
* There is also `emergencyRebalance` function in the Vault's code that is callable by the admin, and will help to roll epoch in case the liquidity check fails. Successfully rolling the last epoch allows to remove the full remaining deposit of the last LP. However, the remaining amount they can withdraw already will be very diminished at that point. (Also, users cannot fix the `rollEpoch` problem on their own, without admin help, as far as I could see! A donation of base or side tokens simply pushes the share price up, so that the donation is shared between all LPs still in the vault, but rolling the epoch still fails.)

Additionally, the issue identified by Trust Security is about the the DVP or IG products. In contrast, the present issue considers the payoffs of the LPs. The PoC below shows that interactions between LPs and vault are sufficient to trigger it.

## Impact

If there are multiple depositors, the losses are not shared between them evenly. This can even be used by the more strategic LPs, allowing them to reduce their losses by stealing from other LPs.

## Code Snippet

https://github.com/sherlock-audit/2024-02-smilee-finance/blob/main/smilee-v2-contracts/src/Vault.sol#L698
https://github.com/sherlock-audit/2024-02-smilee-finance/blob/main/smilee-v2-contracts/src/Vault.sol#L714
https://github.com/sherlock-audit/2024-02-smilee-finance/blob/main/smilee-v2-contracts/src/Vault.sol#L264

## Proof of concept


<details>
  <summary>Test case</summary>

```solidity
    function testSlippageTwoDepositors() public {
        // Alice is a big depositor, Bob is a much smaller one
        // Alice here emulates the aggregate behavior of several LPs

        // first, get some tokens for Alice and approve them for the vault
        vm.prank(tokenAdmin);
        TestnetToken(baseToken).mint(alice, 10e18);
        vm.prank(alice);
        TestnetToken(baseToken).approve(address(vault), type(uint256).max);

        // then Bob deposits
        VaultUtils.addVaultDeposit(bob, 1e18, tokenAdmin, address(vault), vm);

        // set nonzero slippage
        TestnetSwapAdapter exchange = TestnetSwapAdapter(AddressProvider(vault.addressProvider()).exchangeAdapter());
        vm.prank(tokenAdmin);
        exchange.setSlippage(0.01e18, 0, 0);  // 1% constant slippage

        // now Alice deposits and withdraws repeatedly
        for (uint256 i = 0; i < 18; ++i) {
            uint256 alicesBalance = baseToken.balanceOf(alice);
            vm.prank(alice);
            vault.deposit(alicesBalance, alice, 0);

            Utils.skipDay(true, vm);
            vm.prank(tokenAdmin);
            vault.rollEpoch();

            // Alice wants to get the full deposit back
            (uint256 heldByAccountAlice, uint256 heldByVaultAlice) = vault.shareBalances(alice);
            uint256 totalSharesAlice = heldByAccountAlice + heldByVaultAlice;

            vm.prank(alice);
            vault.initiateWithdraw(totalSharesAlice);

            Utils.skipDay(true, vm);
            vm.prank(tokenAdmin);
            vault.rollEpoch();

            vm.prank(alice);
            vault.completeWithdraw();
        }

        // set zero slippage, so that Bob can get 100% of shares back
        // (otherwise rollEpoch would fail)
        vm.prank(tokenAdmin);
        exchange.setSlippage(0, 0, 0);

        (uint256 heldByAccountBob, uint256 heldByVaultBob) = vault.shareBalances(bob);
        uint256 totalSharesBob = heldByAccountBob + heldByVaultBob;
        vm.prank(bob);
        vault.initiateWithdraw(totalSharesBob);

        Utils.skipDay(true, vm);
        vm.prank(tokenAdmin);
        vault.rollEpoch();

        vm.prank(bob);
        vault.completeWithdraw();

        console.log("vault balances:", baseToken.balanceOf(address(vault)));
        console.log("alice balances:", baseToken.balanceOf(address(alice)));
        console.log("bob balances:  ", baseToken.balanceOf(address(bob)));
    }
```

Output:
```text
[PASS] testSlippageTwoDepositors() (gas: 11179334)
Logs:
  vault balances: 0
  alice balances: 9181197934764305740
  bob balances:   87657418300968478
```

Since Alice started with `10e8` and Bob started with `1e18` tokens, it's clear that Bob has been disproportionally affected by the losses, both in absolute and in relative terms.

* Alice lost `0.819e+18` tokens (8.19% of her initial deposit).
* Bob lost `0.916e18` tokens (91.6% if his initial deposit).

Since Alice's liquidity was in the vault only in every second epoch, under some interpretation it might be fair for her to lose less relative to her initial deposit than Bob loses relative to his. However, it's not fair that Bob loses an order of magnitude more! Under a different interpretation, Alice should in fact bear most of the losses in % terms, as she is the one that causes the vault to be frequently rebalanced.

</details>

## Tool used

Manual Review

## Recommendation

Review the way slippage is handled by the protocol.

(Idea, requires some more thought.) If you're willing to make the protocol itself compensate the LPs for DEX slippage (which may create security risks on its own, so must be considered carefully), then one suggestion would be to introduce an additional account, or potentially an `uint256` variable for internal accounting, which serves as a buffer for minor differences in the expected and real token outputs from swaps. This would allows to make all swap outputs deterministic given a fixed oracle price or a DEX quote. If the slippage is negative, tokens are taken from the buffer. If positive, the extra tokens are sent to the buffer. The variable is initialized on deployment, when some small amount of tokens must be deposited in the contract, as the initial buffer, and can never go negative â€” if there's not enough liquidity in the buffer, operations simply revert.