Interesting Lace Cottonmouth

high

# No slippage parameters or deadline when swapping on UniswapV3

## Summary
The `UniswapAdapter.sol` contract is an adapter to connect with uniswap pools. When swapping in, there is no slippage protection, in addition to all swaps setting `block.timestamp` as the deadline, which indicates that there is no deadline set either. This allows massive loss of funds due to attacks such as MEV bot sandwich attacks.

## Vulnerability Detail
In all instances in `UniswapAdapter.sol` that consists of "swapping in", the `amountOutMinimum` is set to 0. This indicates that there is no slippage protection when swapping in, which can lead to sandwich attacks. It is important to note that this is not the case when "swapping out", which does have slippage protection, through a user specified `preApprovedInput`. 

However, all swaps have the deadline set to `block.timestamp`. This means that there is no deadline on swaps, since the deadline is checked against the `block.timestamp` (`deadline >= block.timestamp`).

Due to lack of slippage and deadline, swaps are highly susceptible to attacks that can cause significant loss of funds, such as sandwich attacks.

## Impact
Loss of funds and not receiving the correct amount of tokens.

## Code Snippet
`UniswapAdapter::swapIn` [#L130-141](https://github.com/sherlock-audit/2024-02-smilee-finance/blob/main/smilee-v2-contracts/src/providers/uniswap/UniswapAdapter.sol#L130-L141)
```javascript
    function swapIn(address tokenIn, address tokenOut, uint256 amountIn) public returns (uint256 tokenOutAmount) {
        _zeroAddressCheck(tokenIn);
        _zeroAddressCheck(tokenOut);

        TransferHelper.safeTransferFrom(tokenIn, msg.sender, address(this), amountIn);
        TransferHelper.safeApprove(tokenIn, address(_swapRouter), amountIn);

        TimeLockedSwapPath storage path = _swapPaths[_encodePair(tokenIn, tokenOut)];
        tokenOutAmount = path.exists.get()
            ? _swapInPath(path.data.get(), amountIn)
            : _swapInSingle(tokenIn, tokenOut, amountIn);
    }
```
As you can see, `swapIn` does not accept a `amountOutMinimum` slippage parameter. In the next two functions you will see that it is set to 0. In addition, you will see that all swap functions don't have a deadline (set to `block.timestamp`).

`UniswapAdapter::_swapInSingle` [#L170-187](https://github.com/sherlock-audit/2024-02-smilee-finance/blob/main/smilee-v2-contracts/src/providers/uniswap/UniswapAdapter.sol#L170-L187)
```javascript
    function _swapInSingle(
        address tokenIn,
        address tokenOut,
        uint256 amountIn
    ) private returns (uint256 tokenOutAmount) {
        ISwapRouter.ExactInputSingleParams memory params = ISwapRouter.ExactInputSingleParams(
            tokenIn,
            tokenOut,
            _DEFAULT_FEE,
            msg.sender,
@>          block.timestamp,
            amountIn,
@>          0,
            _SQRTPRICELIMITX96
        );

        tokenOutAmount = _swapRouter.exactInputSingle(params);
    }
```

`UniswapAdapter::_swapInPath` [#L190-200](https://github.com/sherlock-audit/2024-02-smilee-finance/blob/main/smilee-v2-contracts/src/providers/uniswap/UniswapAdapter.sol#L190-L200)
```javascript
    function _swapInPath(bytes memory path, uint256 amountIn) private returns (uint256 tokenOutAmount) {
        ISwapRouter.ExactInputParams memory params = ISwapRouter.ExactInputParams(
            path,
            msg.sender,
@>          block.timestamp,
            amountIn,
@>          0
        );

        tokenOutAmount = _swapRouter.exactInput(params);
    }
```

`UniswapAdapter::_swapOutSingle` [#L203-211](https://github.com/sherlock-audit/2024-02-smilee-finance/blob/main/smilee-v2-contracts/src/providers/uniswap/UniswapAdapter.sol#L203-L221)
```javascript
    function _swapOutSingle(
        address tokenIn,
        address tokenOut,
        uint256 amountOut,
        uint256 amountMaximumIn
    ) private returns (uint256 amountIn) {
        ISwapRouter.ExactOutputSingleParams memory params = ISwapRouter.ExactOutputSingleParams(
            tokenIn,
            tokenOut,
            _DEFAULT_FEE,
            msg.sender,
 @>         block.timestamp,
            amountOut,
            amountMaximumIn,
            _SQRTPRICELIMITX96
        );

        amountIn = _swapRouter.exactOutputSingle(params);
    }
```
`UniswapAdapter::_swapOutPath` [#L224-235](https://github.com/sherlock-audit/2024-02-smilee-finance/blob/main/smilee-v2-contracts/src/providers/uniswap/UniswapAdapter.sol#L224-L235)
```javascript
    function _swapOutPath(
        bytes memory path,
        uint256 amountOut,
        uint256 amountMaximumIn
    ) private returns (uint256 amountIn) {
        ISwapRouter.ExactOutputParams memory params = ISwapRouter.ExactOutputParams(
            path,
            msg.sender,
@>          block.timestamp,
            amountOut,
            amountMaximumIn
        );
```

## Tool used
Manual Review

## Recommendation
Allow users to specify `amountOutMinimum` and `deadline`.