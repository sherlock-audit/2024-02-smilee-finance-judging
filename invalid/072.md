Bent Crimson Goat

medium

# Incorrect fee estimates result in denial of service when minting via the `PositionManager`.

## Summary

When minting a position via the [`PositionManager`](https://github.com/sherlock-audit/2024-02-smilee-finance/blob/main/smilee-v2-contracts/src/periphery/PositionManager.sol), the estimated premium is calculated using [`premium(uint256,uint256,uint256)`](https://github.com/sherlock-audit/2024-02-smilee-finance/blob/3241f1bf0c8e951a41dd2e51997f64ef3ec017bd/smilee-v2-contracts/src/IG.sol#L94) using the off-chain [`IPriceOracle`](https://github.com/sherlock-audit/2024-02-smilee-finance/blob/main/smilee-v2-contracts/src/interfaces/IPriceOracle.sol) data feed as a reference.

At execution time, however, the realized `premium` may be greater than requested. This is due to the dependence on the external state of the UniswapV3 liquidity pool and the [`DVP`](https://github.com/sherlock-audit/2024-02-smilee-finance/blob/3241f1bf0c8e951a41dd2e51997f64ef3ec017bd/smilee-v2-contracts/src/DVP.sol)'s preference to execute at the highest price to avoid potential price manipulation attack vectors.

Although the higher premium may not exceed the caller's specified [`maxSlippage`](https://github.com/sherlock-audit/2024-02-smilee-finance/blob/3241f1bf0c8e951a41dd2e51997f64ef3ec017bd/smilee-v2-contracts/src/DVP.sol#L237) limits, it can still exceed the [`PositionManager`](https://github.com/sherlock-audit/2024-02-smilee-finance/blob/main/smilee-v2-contracts/src/periphery/PositionManager.sol)'s approved spend amount, causing the entire transaction to `revert`.

## Vulnerability Detail

When [`mint`](https://github.com/sherlock-audit/2024-02-smilee-finance/blob/3241f1bf0c8e951a41dd2e51997f64ef3ec017bd/smilee-v2-contracts/src/periphery/PositionManager.sol#L91)ing a new position, the [`PositionManager`](https://github.com/sherlock-audit/2024-02-smilee-finance/blob/main/smilee-v2-contracts/src/periphery/PositionManager.sol) calculates the fees which need to be transferred to the [`DVP`](https://github.com/sherlock-audit/2024-02-smilee-finance/blob/3241f1bf0c8e951a41dd2e51997f64ef3ec017bd/smilee-v2-contracts/src/DVP.sol) by consulting the [`premium(uint256,uint256,uint256)`](https://github.com/sherlock-audit/2024-02-smilee-finance/blob/3241f1bf0c8e951a41dd2e51997f64ef3ec017bd/smilee-v2-contracts/src/IG.sol#L94) function, which returns the fee-inclusive `obtainedPremium` required to mint the position:

```solidity
uint256 obtainedPremium;
uint256 fee;

/// @audit Calculates the premium to pay for this position. Assumes `fee`
/// is returned inside the `obtainedPremium`.
(obtainedPremium, fee) = dvp.premium(params.strike, params.notionalUp, params.notionalDown);

// Transfer premium:
// NOTE: The PositionManager is just a middleman between the user and the DVP
/// @audit Is the premium denominated in `baseFee`?
IERC20 baseToken = IERC20(dvp.baseToken());
baseToken.safeTransferFrom(msg.sender, address(this), obtainedPremium);

/// @audit Is that correct?
// Premium already include fee
baseToken.safeApprove(params.dvpAddr, obtainedPremium);
```

Notice that prior to minting, the [`DVP`](https://github.com/sherlock-audit/2024-02-smilee-finance/blob/3241f1bf0c8e951a41dd2e51997f64ef3ec017bd/smilee-v2-contracts/src/DVP.sol) (in our case, the [`IG`](https://github.com/sherlock-audit/2024-02-smilee-finance/blob/main/smilee-v2-contracts/src/IG.sol)) is approved the precise `obtainedPremium`, which depends upon the current oracle price:

```solidity
/// @inheritdoc IDVP
function premium(
    uint256 strike,
    uint256 amountUp,
    uint256 amountDown
) public view virtual override returns (uint256 premium_, uint256 fee) {
    strike;

@>  uint256 price = IPriceOracle(_getPriceOracle()).getPrice(sideToken, baseToken);
    Amount memory amount_ = Amount({up: amountUp, down: amountDown});

@>  premium_ = _getMarketValue(financeParameters.currentStrike, amount_, true, price);
    (fee, ) = IFeeManager(_getFeeManager()).tradeBuyFee(
        address(this),
        getEpoch().current,
        amountUp + amountDown,
@>      premium_,
        _baseTokenDecimals
    );
    premium_ += fee;
}
```

Back over on the [`DVP`](https://github.com/sherlock-audit/2024-02-smilee-finance/blob/3241f1bf0c8e951a41dd2e51997f64ef3ec017bd/smilee-v2-contracts/src/DVP.sol), the transaction is not guaranteed to be executed at the oracle price, and can instead be executed at the pool price (if it is greater):

```solidity
uint256 swapPrice = _deltaHedgePosition(strike, amount, true);
uint256 premiumOrac = _getMarketValue(strike, amount, true, IPriceOracle(_getPriceOracle()).getPrice(sideToken, baseToken));
uint256 premiumSwap = _getMarketValue(strike, amount, true, swapPrice);
premium_ = premiumSwap > premiumOrac ? premiumSwap : premiumOrac;
```

This means during normal flow, it is quite plausible for the [`DVP`](https://github.com/sherlock-audit/2024-02-smilee-finance/blob/3241f1bf0c8e951a41dd2e51997f64ef3ec017bd/smilee-v2-contracts/src/DVP.sol) to attempt to spend an amount of input tokens from the [`PositionManager`](https://github.com/sherlock-audit/2024-02-smilee-finance/blob/main/smilee-v2-contracts/src/periphery/PositionManager.sol) greater than what has been approved, resulting in a `revert`.

## Impact

Medium. This issue can prevent users from being able to create or modify existing positions in a timely manner, resulting in a potential loss of realized profit due to inoptimal (or even impossible) trade execution, which can impact the ability for the strategy to rebalance in a timely manner.

Although bypassing the [`PositionManager`](https://github.com/sherlock-audit/2024-02-smilee-finance/blob/main/smilee-v2-contracts/src/periphery/PositionManager.sol) by interacting with the [`DVP`](https://github.com/sherlock-audit/2024-02-smilee-finance/blob/3241f1bf0c8e951a41dd2e51997f64ef3ec017bd/smilee-v2-contracts/src/DVP.sol) is a viable workaround for new positions, this is not a compatible solution for modifying positions that have already been created using the [`PositionManager`](https://github.com/sherlock-audit/2024-02-smilee-finance/blob/main/smilee-v2-contracts/src/periphery/PositionManager.sol).

## Code Snippets

```solidity
/// @inheritdoc IDVP
function premium(
    uint256 strike,
    uint256 amountUp,
    uint256 amountDown
) public view virtual override returns (uint256 premium_, uint256 fee) {
    strike;

    uint256 price = IPriceOracle(_getPriceOracle()).getPrice(sideToken, baseToken);
    Amount memory amount_ = Amount({up: amountUp, down: amountDown});

    premium_ = _getMarketValue(financeParameters.currentStrike, amount_, true, price);
    (fee, ) = IFeeManager(_getFeeManager()).tradeBuyFee(
        address(this),
        getEpoch().current,
        amountUp + amountDown,
        premium_,
        _baseTokenDecimals
    );
    premium_ += fee;
}
```

## Tool used

Manual Review

## Recommendation

The [`PositionManager`](https://github.com/sherlock-audit/2024-02-smilee-finance/blob/main/smilee-v2-contracts/src/periphery/PositionManager.sol) should approve and transfer the [`DVP`](https://github.com/sherlock-audit/2024-02-smilee-finance/blob/3241f1bf0c8e951a41dd2e51997f64ef3ec017bd/smilee-v2-contracts/src/DVP.sol#L23) the maximum allowance permitted by the caller's [`maxSlippage`](https://github.com/sherlock-audit/2024-02-smilee-finance/blob/3241f1bf0c8e951a41dd2e51997f64ef3ec017bd/smilee-v2-contracts/src/DVP.sol#L237).

This will ensure greater resistance to fluctuations in pool price compared to the oracle price, at no additional cost to the caller, since any funds that are not used in the process will be returned via the existing refund mechanism.

Failing this, it is recommended that the [`premium(uint256,uint256,uint256)`](https://github.com/sherlock-audit/2024-02-smilee-finance/blob/3241f1bf0c8e951a41dd2e51997f64ef3ec017bd/smilee-v2-contracts/src/IG.sol#L94) function incorporates price prediction logic consistent with the price mechanisms at execution time via the consultation of a UniswapV3 pool quote.